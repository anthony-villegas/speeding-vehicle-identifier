/**
 * Encoder to distance template for section 7.2.3.
 */
target C;

reactor EncoderToDistance(
    ticks_to_meters:float(0.0006108),
    init_encoder:int(300000)
) {
    input encoder:uint16_t;   // Ticks.
    output distance:float;    // Meters.
    
    reset state previous_encoder:uint16_t(0);
    reset state previous_distance:float(0);
    reset state first:bool(true);   // Treat first encoder input specially.
    
    reaction (encoder) -> distance {=
        if (self->first) {
            // First encoder input, so distance should be zero.
            self->previous_encoder = encoder->value;
            self->first = false;
            lf_set(distance, 0);
        } 
        else {
            int16_t diff =  (int16_t) (encoder->value - self->previous_encoder);
            float dist = self->previous_distance + (diff * self->ticks_to_meters);
            SET(distance, dist);
            self->previous_distance = dist;
            self->previous_encoder = encoder->value;

        }    
    =}
}